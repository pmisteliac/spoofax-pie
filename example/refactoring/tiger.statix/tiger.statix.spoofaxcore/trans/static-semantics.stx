module static-semantics

signature
  sorts Id = string

signature
  sorts Module Exp
  constructors
    Mod : Exp -> Module

rules // single-file entry point

  programOK : Module

  programOK(Mod(e)) :- {s T}
    new s, init(s),
    typeOfExp(s, e) == T.

rules // multi-file entry point

//  projectOk : scope
//  projectOk(s).
//  fileOk : scope * Start
//  fileOk(s, Empty()).

signature // names
  namespaces
    Loop  : string
    Field : string

signature // names
  name-resolution
    labels R
    resolve Loop  filter P* min $ < P
    resolve Field filter P* min $ < P


signature
  relations
    typeOfDecl : occurrence -> TYPE // :
  namespaces
    Var  : string
    Type : string
  name-resolution
    labels P
    resolve Var  filter P* min $ < P
    resolve Type filter P* min $ < P


rules // type binding

  declareType : scope * string * TYPE

  declareType(s, x, T) :-
    s -> Type{x} with typeOfDecl T,
    // declaration is distinct
    typeOfDecl of Type{x} in s |-> [(_, (_, T))].

  declareVar : scope * string * TYPE

  declareVar(s, x, T) :-
    s -> Var{x} with typeOfDecl T,
    // declaration is distinct
    typeOfDecl of Var{x} in s |-> [(_, (_, T))].



  declareTypeRaw : scope * string * TYPE
  lookupType : scope * string -> TYPE

  declareTypeRaw(s, x, T) :-
    s -> Type{x} with typeOfDecl T,
    typeOfDecl of Type{x} in s |-> [(_, (_, T))].
    // declaration is distinct

  lookupType(s, x) = T :- {x'} @x.ref := x',
    typeOfDecl of Type{x} in s |-> [(_, (Type{x'}, T))|_].
    // permissive lookup to cope with double declaration

rules // variable binding

  declareVarRaw : scope * string * TYPE
  lookupVar : scope * string -> TYPE

  declareVarRaw(s, x, T) :-
    s -> Var{x} with typeOfDecl T,
    // declaration is distinct
    typeOfDecl of Var{x} in s |-> [(_, (_, T))].

  lookupVar(s, x) = T :- {x'} @x.ref := x',
    typeOfDecl of Var{x} in s |-> [(_, (Var{x'}, T))|_].
    // permissive lookup to cope with double declaration

rules // field binding

  declareField : scope * TypeId * TYPE
  lookupField  : scope * string -> TYPE

  declareField(s, x, T) :-
    s -> Field{x} with typeOfDecl T,
    // declaration is distinct
    typeOfDecl of Field{x} in s |-> [(_, (_, T))].

  lookupField(s, x) = T :- {x'} @x.ref := x',
    typeOfDecl of Field{x} in s |-> [(_, (Field{x'}, T))|_].
    // permissive lookup to cope with double declaration

signature
  sorts TYPE
  constructors
    UNIT   : TYPE
    INT    : TYPE
    STRING : TYPE
    NIL    : TYPE
    RECORD : scope -> TYPE
    ARRAY  : TYPE * scope -> TYPE
    FUN    : list(TYPE) * TYPE -> TYPE

rules subtype : TYPE * TYPE

  subtype(T, T).

  subtype(NIL(), RECORD(s)).

rules subtypeO : Type * TYPE * TYPE

  subtypeO(t, T, T).

  subtypeO(t, NIL(), RECORD(s)).

rules subtypes : list(TYPE) * list(TYPE)

//  subtypes maps subtype(list(*), list(*))

  subtypes([], []).

  subtypes([T | Ts], [S | Ss]) :-
    subtype(T, S),
    subtypes(Ts, Ss).

rules equitype : TYPE * TYPE

  equitype(T, T).
  equitype(NIL(), RECORD(s)).
  equitype(RECORD(s), NIL()).

  lub : TYPE * TYPE -> TYPE
  lub(S, T) = S.
  lub(NIL(), T) = T.

rules

  typeOfExp  : scope * Exp -> TYPE
  typeOfExps   maps typeOfExp(*, list(*)) = list(*)


  typeOfSeq  : scope * list(Exp) -> TYPE
  typeOfLVal : scope * LValue -> TYPE

signature
  sorts Declarations
  constructors
    Let          : list(Dec) * list(Exp) -> Exp
    Declarations : list(Dec) -> Declarations

rules

  typeOfExp(s_outer, Let(ds, es)) = T :- {s_body}
    new s_body,
    decsOk(s_outer, s_body, ds),
    typeOfSeq(s_body, es) == T.


  decsOk     : scope * scope * list(Dec)
  decOk      : scope * scope * Dec

  decsOk(s_outer, s_body, []) :-
    s_body -P-> s_outer.

  decsOk(s_outer, s_body, [dec]) :-
    s_body -P-> s_outer,
    decOk(s_body, s_outer, dec).

  decsOk(s_outer, s_body, [dec | decs@[_|_]]) :- {s_dec}
    new s_dec,
    s_dec -P-> s_outer,
    decOk(s_dec, s_outer, dec),
    decsOk(s_dec, s_body, decs).

signature
  sorts TypeDec
  sorts TypeId = Id
  constructors
    TypeDecs  : list(TypeDec) -> Dec
    TypeDec   : TypeId * Type -> TypeDec
    Tid       : TypeId -> Type

rules // type declarations

  // Types: In the expression [let ... typedecs ... in exps end] the
  // scope of a type identifier starts at the beginning of the
  // consecutive sequence of type declarations defining it and lasts
  // until the end. The includes the headers and bodies of any functions
  // with the scope.

  // Name spaces: There are two different name spaces: one for types,
  // and one for functions and variables. A type [a] can be "in scope"
  // at the same time as a variable [a] or a function [a], but
  // variables and functions of the same name cannot both be in
  // scope simultaneously (one will hide the other).

  decOk(s, s_outer, TypeDecs(tdecs)) :-
    tdecsOk(s, s, tdecs).

  tdecOk  : scope * scope * TypeDec
  tdecsOk   maps tdecOk(*, *, list(*))

  tdecOk(s, s_outer, TypeDec(x, t)) :- {T}
    typeOfType(s_outer, t) == T,
    declareType(s, x, T).

   // note: type declarations in a sequence are mutually recursive

rules // types

  typeOfType : scope * Type -> TYPE

  typeOfType(s, Tid(x)) = lookupType(s, x).

    // typeOfDecl of Type{x} in s |-> [(_, (_, T))|_].
    // permissive query to allow non-distinct type declarations

signature
  sorts Dec FunDec FArg
  constructors
    FunDecs  : list(FunDec) -> Dec
    ProcDec  : Id * list(FArg) * Exp -> FunDec
    FunDec   : Id * list(FArg) * Type * Exp -> FunDec
    FArg     : Id * Type -> FArg
    Call     : Id * list(Exp) -> Exp

rules // function declarations

  // Parameters: In [function id(... id1: id2 ...) = exp] the
  // scope of the parameter id1 lasts throughout the function
  // body exp

  decOk(s, s_outer, FunDecs(fdecs)) :-
    fdecsOk(s, s_outer, fdecs).

  fdecsOk      maps fdecOk(*, *, list(*))
  fdecOk     : scope * scope * FunDec

  fdecOk(s, s_outer, d@ProcDec(f, args, e)) :- {s_fun Ts}
    new s_fun, s_fun -P-> s,
    typesOfArgs(s_fun, s_outer, args) == Ts,
    declareVar(s, f, FUN(Ts, UNIT())),
    typeOfExp(s_fun, e) == UNIT().

  fdecOk(s, s_outer, d@FunDec(f, args, t, e)) :- {s_fun Ts T S}
    new s_fun, s_fun -P-> s,
    typesOfArgs(s_fun, s_outer, args) == Ts,
    typeOfType(s_outer, t) == T,
    declareVar(s, f, FUN(Ts, T)),
    typeOfExp(s_fun, e) == S,
    subtypeO(t, S, T).

rules
  typeOfArg  : scope * scope * FArg -> TYPE
  typesOfArgs  maps typeOfArg(*, *, list(*)) = list(*)

  typeOfArg(s_fun, s_outer, FArg(x, t)) = T :-
    typeOfType(s_outer, t) == T,
    declareVar(s_fun, x, T).

rules // function calls

  typeOfExp(s, Call(f, es)) = T :- {Ts Ss}
    lookupVar(s, f) == FUN(Ts, T),
    typeOfExps(s, es) == Ss,
    subtypes(Ss, Ts).

signature
  sorts Var
  constructors
    VarDec       : Id * Type * Exp -> Dec
    VarDecNoType : Id * Exp -> Dec
    Var          : Id -> Var
    V2L          : Var -> LValue
    L2E          : LValue -> Exp
    Assign       : LValue * Exp -> Exp

rules // variable declarations

  // Local variables: In the expression [let ... vardec ... in exp end],
  // the scope of the declared variable starts just after its vardec
  // and lasts until the end.

  decOk(s, s_outer, VarDec(x, t, e)) :- {T S}
    typeOfType(s_outer, t) == T,
    typeOfExp(s_outer, e) == S,
    subtype(S, T),
    declareVar(s, x, T).

  decOk(s, s_outer, VarDecNoType(x, e)) :- {T}
    typeOfExp(s_outer, e) == T,
    T != NIL(),
    declareVar(s, x, T).

rules // variable references

  typeOfExp(s, L2E(V2L(Var(x)))) = lookupVar(s, x).

signature
  sorts LValue Type
  constructors
    ArrayTy      : TypeId -> Type
    Array        : TypeId * Exp * Exp -> Exp
    Subscript    : LValue * Exp -> LValue

rules // arrays

  typeOfType(s, ArrayTy(x)) = ARRAY(T, s_arr) :-
    new s_arr, // unique token to distinghuish the array type
    typeOfType(s, Tid(x)) == T.

rules // array creation

  typeOfExp(s, Array(x, e1, e2)) = ARRAY(T, s_arr) :- {S}
    typeOfType(s, Tid(x)) == ARRAY(S, s_arr),
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == T,
    subtype(T, S).

rules // array indexing

  typeOfLVal(s, Subscript(e1, e2)) = T :- {s_arr}
    typeOfLVal(s, e1) == ARRAY(T, s_arr),
    typeOfExp(s, e2) == INT().

rules // statements

  typeOfExp(s, Assign(e1, e2)) = UNIT() :- {T S}
    typeOfLVal(s, e1) == T,
    typeOfExp(s, e2) == S,
    subtype(S, T).

rules

  typeOfLVal(s, V2L(Var(x))) = T :-
    lookupVar(s, x) == T.

  typeOfExp(s, L2E(lval)) = typeOfLVal(s, lval).

signature
  constructors
    Seq           : list(Exp) -> Exp
    If            : Exp * Exp * Exp -> Exp
    IfThen        : Exp * Exp -> Exp
    While         : Exp * Exp -> Exp
    For           : Var * Exp * Exp * Exp -> Exp
    Break         : Exp

rules // sequence

  typeOfSeq(s, []) = UNIT().

  typeOfSeq(s, [e]) = T :-
    typeOfExp(s, e) == T.

  typeOfSeq(s, [e | es@[_|_]]) = T :- {S}
    typeOfExp(s, e) == S,
    typeOfSeq(s, es) == T.

  typeOfExp(s, Seq(es)) = typeOfSeq(s, es).

  typeOfExp(s, If(e1, e2, e3)) = lub(T, S) :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == T,
    typeOfExp(s, e3) == S,
    equitype(S, T).

  typeOfExp(s, IfThen(e1, e2)) = UNIT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == UNIT().

  typeOfExp(s, While(e1, e2)) = UNIT() :- {s_loop}
    new s_loop, s_loop -P-> s,
    s_loop -> Loop{"break"} with typeOfDecl UNIT(),
    typeOfExp(s_loop, e1) == INT(),
    typeOfExp(s_loop, e2) == UNIT().

  typeOfExp(s, For(Var(x), e1, e2, e3)) = UNIT() :- {s_for}
    new s_for,
    s_for -P-> s,
    declareVar(s_for, x, INT()),
    s_for -> Loop{"break"} with typeOfDecl UNIT(),
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT(),
    typeOfExp(s_for, e3) == UNIT().

  typeOfExp(s, Break()) = UNIT() :-
    typeOfDecl of Loop{"break"} in s |-> [(_, (_, UNIT()))].

signature
   sorts IntConst = string
   sorts RealConst = string
   sorts Sign = string
  constructors
    Int            : IntConst -> Exp
    Uminus         : Exp -> Exp
    Times          : Exp * Exp -> Exp
    Divide         : Exp * Exp -> Exp
    Plus           : Exp * Exp -> Exp
    Minus          : Exp * Exp -> Exp
    Eq             : Exp * Exp -> Exp
    Neq            : Exp * Exp -> Exp
    Gt             : Exp * Exp -> Exp
    Lt             : Exp * Exp -> Exp
    Geq            : Exp * Exp -> Exp
    Leq            : Exp * Exp -> Exp
    And            : Exp * Exp -> Exp
    Or             : Exp * Exp -> Exp
    Exp-Plhdr      : Exp

rules // literals

  typeOfExp(s, Int(i)) = INT().

rules // operators

  typeOfExp(s, Uminus(e)) = INT() :-
    typeOfExp(s, e) == INT().

  typeOfExp(s, Divide(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Times(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Minus(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Plus(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Eq(e1, e2)) = INT() :- {T S}
    typeOfExp(s, e1) == T,
    typeOfExp(s, e2) == S,
    equitype(T, S).
    // TODO: does Eq work for all types?

  typeOfExp(s, Neq(e1, e2)) = INT() :- {T S}
    typeOfExp(s, e1) == T,
    typeOfExp(s, e2) == S,
    equitype(T, S).
    // TODO: does Neq work for all types?

  typeOfExp(s, Gt(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().
    // TODO: does Gt work for more types?

  typeOfExp(s, Lt(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Geq(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Leq(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, Or(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

  typeOfExp(s, And(e1, e2)) = INT() :-
    typeOfExp(s, e1) == INT(),
    typeOfExp(s, e2) == INT().

signature
  sorts Field InitField
  constructors
    RecordTy   : list(Field) -> Type
    Field      : Id * TypeId -> Field
    NilExp     : Exp
    Record     : TypeId * list(InitField) -> Exp
    InitField  : Id * Exp -> InitField
    FieldVar   : LValue * Id -> LValue

rules // record type

  typeOfType(s, RecordTy(fields)) = RECORD(s_rec) :-
    new s_rec,
    fieldsOk(s_rec, s, fields).

  fieldOk : scope * scope * Field
  fieldsOk maps fieldOk(*, *, list(*))

  fieldOk(s_rec, s_outer, Field(x, t)) :- {T}
    typeOfType(s_outer, Tid(t)) == T,
    declareField(s_rec, x, T).

rules // literals

  typeOfExp(s, NilExp()) = NIL().

rules // record creation

  typeOfExp(s, e@Record(t, inits)) = RECORD(s_rec) :- {s_init ds}
    typeOfType(s, Tid(t)) == RECORD(s_rec),
    new s_init,
    initsOk(s, s_rec, s_init, inits),
    typeOfDecl of Field{_} in s_rec |-> ds,
    allFieldsInitialized(t, ds, s_init).

  initOk : scope * scope * scope * InitField
  initsOk maps initOk(*, *, *, list(*))

  initOk(s, s_rec, s_init, InitField(x, e)) :- {S T}
    lookupField(s_rec, x) == T,
    typeOfExp(s, e) == S,
    declareField(s_init, x, S),
    subtype(S, T).

  fieldInitialized : TypeId * (path * (occurrence * TYPE)) * scope
  allFieldsInitialized maps fieldInitialized(*, list(*), *)

  fieldInitialized(t, (_, (Field{x}, _)), s) :-
    typeOfDecl of Field{x} in s |-> [_].
    // t is passed such that error is displayed be on t;
    // noting that init of x is missing

rules // record field access

  typeOfLVal(s, FieldVar(lval, x)) = T :- {s_rec}
    typeOfLVal(s, lval) == RECORD(s_rec),
    lookupField(s_rec, x) == T.

signature
  sorts StrConst = string
  constructors
    String : StrConst -> Exp

rules // literals

  typeOfExp(s, String(_)) = STRING().

rules

  init : scope

  init(s) :-
    declareTypeRaw(s, "int",    INT()),
    declareTypeRaw(s, "string", STRING()),

    declareVarRaw(s,  "print",     FUN([STRING()], UNIT())),
    declareVarRaw(s,  "chr",       FUN([INT()], STRING())),
    declareVarRaw(s,  "ord",       FUN([STRING()], INT())),
    declareVarRaw(s,  "size",      FUN([STRING()], INT())),
    declareVarRaw(s,  "substring", FUN([STRING(), INT(), INT()], STRING())),
    declareVarRaw(s,  "concat",    FUN([STRING(), STRING()], STRING())),
    declareVarRaw(s,  "not",       FUN([INT()], INT())),
    declareVarRaw(s,  "exit",      FUN([INT()], UNIT())),
    declareVarRaw(s,  "getchar",   FUN([], STRING())),
    declareVarRaw(s,  "flush",     FUN([], UNIT())),
    declareVarRaw(s,  "printi",    FUN([INT()], UNIT())).

